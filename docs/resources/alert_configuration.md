---
subcategory: "Alert Configurations"
---

# Resource: mongodbatlas_alert_configuration

`mongodbatlas_alert_configuration` provides an Alert Configuration resource to define the conditions that trigger an alert and the methods of notification within a MongoDB Atlas project.

-> **NOTE:** Groups and projects are synonymous terms. You may find `groupId` in the official documentation.

## Example Usage

```terraform
resource "mongodbatlas_alert_configuration" "test" {
  project_id = "<PROJECT-ID>"
  event_type = "OUTSIDE_METRIC_THRESHOLD"
  enabled    = true

  notification {
    type_name     = "GROUP"
    interval_min  = 5
    delay_min     = 0
    sms_enabled   = false
    email_enabled = true
    roles         = ["GROUP_CLUSTER_MANAGER"]
  }

  matcher {
    field_name = "HOSTNAME_AND_PORT"
    operator   = "EQUALS"
    value      = "SECONDARY"
  }

  metric_threshold_config {
    metric_name = "ASSERT_REGULAR"
    operator    = "LESS_THAN"
    threshold   = 99.0
    units       = "RAW"
    mode        = "AVERAGE"
  }
}
```

-> **NOTE:** In order to allow for a fast pace of change to alert variables some validations have been removed from this resource in order to unblock alert creation. Impacted areas have links to the MongoDB Atlas API documentation so always check it for the most current information: https://docs.atlas.mongodb.com/reference/api/alert-configurations-create-config/


```terraform
resource "mongodbatlas_alert_configuration" "test" {
  project_id = "<PROJECT-ID>"
  event_type = "REPLICATION_OPLOG_WINDOW_RUNNING_OUT"
  enabled    = true

  notification {
    type_name     = "GROUP"
    interval_min  = 5
    delay_min     = 0
    sms_enabled   = false
    email_enabled = true
    roles         = ["GROUP_CLUSTER_MANAGER"]
  }

  matcher {
    field_name = "CLUSTER_NAME"
    operator   = "EQUALS"
    value      = "my-cluster"
  }

  threshold_config {
    operator    = "LESS_THAN"
    threshold   = 1
    units       = "HOURS"
  }
}
```

### Create an alert with two notifications using Email and SMS


```terraform
resource "mongodbatlas_alert_configuration" "test" {
  project_id = "PROJECT ID"
  event_type = "OUTSIDE_METRIC_THRESHOLD"
  enabled    = true

  notification {
    type_name     = "GROUP"
    interval_min  = 5
    delay_min     = 0
    sms_enabled   = false
    email_enabled = true
    roles = ["GROUP_DATA_ACCESS_READ_ONLY", "GROUP_CLUSTER_MANAGER", "GROUP_DATA_ACCESS_ADMIN"]
  }

  notification {
    type_name     = "ORG"
    interval_min  = 5
    delay_min     = 0
    sms_enabled   = true
    email_enabled = false
  }

  matcher {
    field_name = "HOSTNAME_AND_PORT"
    operator   = "EQUALS"
    value      = "SECONDARY"
  }

  metric_threshold_config {
    metric_name = "ASSERT_REGULAR"
    operator    = "LESS_THAN"
    threshold   = 99.0
    units       = "RAW"
    mode        = "AVERAGE"
  }
}
```

### Create third party notification using credentials from existing third party integration


```terraform
data "mongodbatlas_third_party_integration" "test" {
    project_id = "PROJECT ID"
    type = "PAGER_DUTY"
}

resource "mongodbatlas_alert_configuration" "test" {
  project_id = "PROJECT ID"
  enabled    = true
  event_type = "USERS_WITHOUT_MULTI_FACTOR_AUTH"

  notification {
    type_name     = "PAGER_DUTY"
    integration_id = data.mongodbatlas_third_party_integration.test.id
  }
}
```

### Further Examples
- [Alert Configuration](https://github.com/mongodb/terraform-provider-mongodbatlas/tree/v2.7.0/examples/mongodbatlas_alert_configuration)

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `event_type` (String)
- `project_id` (String)

### Optional

- `enabled` (Boolean)
- `matcher` (Block List) (see [below for nested schema](#nestedblock--matcher))
- `metric_threshold_config` (Block List) (see [below for nested schema](#nestedblock--metric_threshold_config))
- `notification` (Block List) (see [below for nested schema](#nestedblock--notification))
- `severity_override` (String)
- `threshold_config` (Block List) (see [below for nested schema](#nestedblock--threshold_config))

### Read-Only

- `alert_configuration_id` (String)
- `created` (String)
- `id` (String) The ID of this resource.
- `updated` (String)

<a id="nestedblock--matcher"></a>
### Nested Schema for `matcher`

Required:

- `field_name` (String)
- `operator` (String)
- `value` (String)


<a id="nestedblock--metric_threshold_config"></a>
### Nested Schema for `metric_threshold_config`

Required:

- `metric_name` (String)

Optional:

- `mode` (String)
- `operator` (String)
- `threshold` (Number)
- `units` (String)


<a id="nestedblock--notification"></a>
### Nested Schema for `notification`

Required:

- `type_name` (String)

Optional:

- `api_token` (String, Sensitive)
- `channel_name` (String)
- `datadog_api_key` (String, Sensitive)
- `datadog_region` (String)
- `delay_min` (Number)
- `email_address` (String)
- `email_enabled` (Boolean)
- `integration_id` (String)
- `interval_min` (Number)
- `microsoft_teams_webhook_url` (String, Sensitive)
- `mobile_number` (String)
- `notifier_id` (String)
- `ops_genie_api_key` (String, Sensitive)
- `ops_genie_region` (String)
- `roles` (List of String)
- `service_key` (String, Sensitive)
- `sms_enabled` (Boolean)
- `team_id` (String)
- `username` (String)
- `victor_ops_api_key` (String, Sensitive)
- `victor_ops_routing_key` (String, Sensitive)
- `webhook_secret` (String, Sensitive)
- `webhook_url` (String, Sensitive)

Read-Only:

- `team_name` (String)


<a id="nestedblock--threshold_config"></a>
### Nested Schema for `threshold_config`

Optional:

- `operator` (String)
- `threshold` (Number)
- `units` (String)

## Import

Alert Configuration can be imported using the `project_id-alert_configuration_id`, e.g.

```
terraform import mongodbatlas_alert_configuration.test 5d0f1f74cf09a29120e123cd-5d0f1f74cf09a29120e1fscg
```

**NOTE**: Third-party notifications will not contain their respective credentials as these are sensitive attributes. If you wish to perform updates on these notifications without providing the original credentials, the corresponding `notifier_id` attribute must be provided instead.

For more information see: [MongoDB Atlas API Reference.](https://docs.atlas.mongodb.com/reference/api/alert-configurations/)
