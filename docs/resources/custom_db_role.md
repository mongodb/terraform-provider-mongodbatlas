---
subcategory: "Database Users"
---

# Resource: mongodbatlas_custom_db_role

`mongodbatlas_custom_db_role` provides a Custom DB Role resource. The customDBRoles resource lets you retrieve, create and modify the custom MongoDB roles in your cluster. Use custom MongoDB roles to specify custom sets of actions which cannot be described by the built-in Atlas database user privileges.

-> **IMPORTANT**  You define custom roles at the project level for all clusters in the project. The `mongodbatlas_custom_db_role` resource supports a subset of MongoDB privilege actions. For a complete list of [privilege actions](https://docs.mongodb.com/manual/reference/privilege-actions/) available for this resource, see [Custom Role actions](https://docs.atlas.mongodb.com/reference/api/custom-role-actions/). Custom roles must include actions that all project's clusters support, and that are compatible with each MongoDB version used by your project's clusters. For example, if your project has MongoDB 4.2 clusters, you can't create custom roles that use actions introduced in MongoDB 4.4.


-> **NOTE:** Groups and projects are synonymous terms. You may find group_id in the official documentation.

## Example Usage

```terraform
resource "mongodbatlas_custom_db_role" "test_role" {
  project_id = "<PROJECT-ID>"
  role_name  = "myCustomRole"

  actions {
    action = "UPDATE"
    resources {
      collection_name = ""
      database_name   = "anyDatabase" # use the empty string ("") to allow an action on all databases.
    }
  }
  actions {
    action = "INSERT"
    resources {
      collection_name = ""
      database_name   = "anyDatabase"
    }
  }
  actions {
    action = "REMOVE"
    resources {
      collection_name = ""
      database_name   = "anyDatabase"
    }
  }
}
```

## Example Usage with inherited roles

```terraform
resource "mongodbatlas_custom_db_role" "inherited_role_one" {
  project_id = "<PROJECT-ID>"
  role_name  = "insertRole"

  actions {
    action = "INSERT"
    resources {
      collection_name = ""
      database_name   = "anyDatabase"
    }
  }
}

resource "mongodbatlas_custom_db_role" "inherited_role_two" {
  project_id = mongodbatlas_custom_db_role.inherited_role_one.project_id
  role_name  = "statusServerRole"

  actions {
    action = "SERVER_STATUS"
    resources {
      cluster = true
    }
  }
}

resource "mongodbatlas_custom_db_role" "test_role" {
  project_id = mongodbatlas_custom_db_role.inherited_role_one.project_id
  role_name  = "myCustomRole"

  actions {
    action = "UPDATE"
    resources {
      collection_name = ""
      database_name   = "anyDatabase"
    }
  }
  actions {
    action = "REMOVE"
    resources {
      collection_name = ""
      database_name   = "anyDatabase"
    }
  }

  inherited_roles {
    role_name     = mongodbatlas_custom_db_role.inherited_role_one.role_name
    database_name = "admin"
  }

  inherited_roles {
    role_name     = mongodbatlas_custom_db_role.inherited_role_two.role_name
    database_name = "admin"
  }
}

```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `project_id` (String)
- `role_name` (String)

### Optional

- `actions` (Block Set) (see [below for nested schema](#nestedblock--actions))
- `inherited_roles` (Block Set) (see [below for nested schema](#nestedblock--inherited_roles))

### Read-Only

- `id` (String) The ID of this resource.

<a id="nestedblock--actions"></a>
### Nested Schema for `actions`

Required:

- `action` (String)
- `resources` (Block Set, Min: 1) (see [below for nested schema](#nestedblock--actions--resources))

<a id="nestedblock--actions--resources"></a>
### Nested Schema for `actions.resources`

Optional:

- `cluster` (Boolean)
- `collection_name` (String)
- `database_name` (String)



<a id="nestedblock--inherited_roles"></a>
### Nested Schema for `inherited_roles`

Required:

- `database_name` (String)
- `role_name` (String)

## Import

Database users can be imported using project ID and username, in the format `PROJECTID-ROLENAME`, e.g.

```
$ terraform import mongodbatlas_custom_db_role.my_role 1112222b3bf99403840e8934-MyCustomRole
```

For more information see: [MongoDB Atlas API Reference.](https://docs.atlas.mongodb.com/reference/api/custom-roles/)