---
subcategory: "Log Integration"
---

# Resource: mongodbatlas_log_integration

`mongodbatlas_log_integration` provides a resource for managing log integration configurations at the project level. This resource allows you to continually export `mongod`, `mongos`, and audit logs to an AWS S3 bucket with 1-minute log export intervals.

To use this resource, the requesting Service Account or API Key must have the Organization Owner or Project Owner role.

## Example Usage

```terraform
resource "mongodbatlas_project" "project" {
  name   = var.atlas_project_name
  org_id = var.atlas_org_id
}

# Set up cloud provider access in Atlas using the created IAM role
resource "mongodbatlas_cloud_provider_access_setup" "setup_only" {
  project_id    = mongodbatlas_project.project.id
  provider_name = "AWS"
}

resource "mongodbatlas_cloud_provider_access_authorization" "auth_role" {
  project_id = mongodbatlas_project.project.id
  role_id    = mongodbatlas_cloud_provider_access_setup.setup_only.role_id

  aws {
    iam_assumed_role_arn = aws_iam_role.atlas_role.arn
  }
}

# Set up log integration with authorized IAM role
resource "mongodbatlas_log_integration" "example" {
  project_id  = mongodbatlas_project.project.id
  bucket_name = aws_s3_bucket.log_bucket.bucket
  iam_role_id = mongodbatlas_cloud_provider_access_authorization.auth_role.role_id
  prefix_path = "atlas-logs"
  type        = "S3_LOG_EXPORT"
  log_types   = ["MONGOD_AUDIT"]
}

data "mongodbatlas_log_integration" "example" {
  project_id     = mongodbatlas_log_integration.example.project_id
  integration_id = mongodbatlas_log_integration.example.integration_id
}

data "mongodbatlas_log_integrations" "example" {
  project_id = mongodbatlas_log_integration.example.project_id
}

output "log_integration_bucket_name" {
  value = data.mongodbatlas_log_integration.example.bucket_name
}

output "log_integrations_results" {
  value = data.mongodbatlas_log_integrations.example.results
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `log_types` (Set of String) Array of log types exported by this integration. The specific log types available and maximum number of items depend on the integration type. See the integration-specific schema for details.
- `project_id` (String) Unique 24-hexadecimal digit string that identifies your project.
- `type` (String) Human-readable label that identifies the service to which you want to integrate with Atlas. The value must match the log integration type.

### Optional

- `api_key` (String, Sensitive) API key for authentication.
- `bucket_name` (String) Name of the bucket to store log files.
- `hec_token` (String, Sensitive) HTTP Event Collector (HEC) token for authentication.
- `hec_url` (String) HTTP Event Collector (HEC) endpoint URL.
- `iam_role_id` (String) Unique 24-character hexadecimal string that identifies the AWS IAM role that Atlas uses to access the S3 bucket.
- `kms_key` (String) AWS KMS key ID or ARN for server-side encryption (optional). If not provided, uses bucket default encryption settings.
- `otel_endpoint` (String) OpenTelemetry collector endpoint URL. Must be HTTPS and not exceed 2048 characters.
- `otel_supplied_headers` (Attributes List, Sensitive) HTTP headers for authentication and configuration. Maximum 10 headers, total size limit 2KB. (see [below for nested schema](#nestedatt--otel_supplied_headers))
- `prefix_path` (String) Path prefix where the log files will be stored. Atlas will add further sub-directories based on the log type.
- `region` (String) Datadog site/region for log ingestion. Valid values: US1, US3, US5, EU, AP1, AP2, US1_FED.
- `role_id` (String) Unique 24-character hexadecimal string that identifies the GCP service account role.
- `service_principal_id` (String) Unique 24-character hexadecimal string that identifies the Service Principal.
- `storage_account_name` (String) Storage account name where logs will be stored.
- `storage_container_name` (String) Storage container name for log files.

### Read-Only

- `integration_id` (String) Unique 24-character hexadecimal digit string that identifies the log integration configuration.

<a id="nestedatt--otel_supplied_headers"></a>
### Nested Schema for `otel_supplied_headers`

Required:

- `name` (String) Header name.
- `value` (String, Sensitive) Header value.

## Import 
Log integration resource can be imported using the project ID and log integration ID, separated by a slash, e.g.

```
$ terraform import mongodbatlas_log_integration.test 650972848269185c55f40ca1/6789abcd1234ef5678901234
```

For more information see: [MongoDB Atlas API - Log Integration](https://www.mongodb.com/docs/api/doc/atlas-admin-api-v2/group/endpoint-push-based-log-export) Documentation.

