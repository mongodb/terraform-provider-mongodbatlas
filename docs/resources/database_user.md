---
subcategory: "Database Users"
---

# Resource: mongodbatlas_database_user

`mongodbatlas_database_user` provides a Database User resource. This represents a database user which will be applied to all clusters within the project.

Each user has a set of roles that provide access to the projectâ€™s databases. User's roles apply to all the clusters in the project: if two clusters have a `products` database and a user has a role granting `read` access on the products database, the user has that access on both clusters.

-> **NOTE:** Groups and projects are synonymous terms. You may find group_id in the official documentation.

~> **WARNING:** The password argument is required for creation but should be removed after creation if it will be managed externally. More details can be found in the password argument documentation.

~> **IMPORTANT:** All arguments including the password will be stored in the raw state as plain-text. [Read more about sensitive data in state.](https://www.terraform.io/docs/state/sensitive-data.html)

## Example Usages

```terraform
resource "mongodbatlas_database_user" "test" {
  username           = "test-acc-username"
  password           = "test-acc-password"
  project_id         = "<PROJECT-ID>"
  auth_database_name = "admin"

  roles {
    role_name     = "readWrite"
    database_name = "dbforApp"
  }

  roles {
    role_name     = "readAnyDatabase"
    database_name = "admin"
  }

  labels {
    key   = "My Key"
    value = "My Value"
  }

  scopes {
    name   = "My cluster name"
    type = "CLUSTER"
  }

  scopes {
    name   = "My second cluster name"
    type = "CLUSTER"
  }
}
```


```terraform
resource "mongodbatlas_database_user" "test" {
  username           = "test-acc-username"
  x509_type          = "MANAGED"
  project_id         = "<PROJECT-ID>"
  auth_database_name = "$external"

  roles {
    role_name     = "readAnyDatabase"
    database_name = "admin"
  }

  labels {
    key   = "%s"
    value = "%s"
  }

  scopes {
    name   = "My cluster name"
    type = "CLUSTER"
  }
}
```

```terraform
resource "mongodbatlas_database_user" "test" {
  username           = aws_iam_role.test.arn
  project_id         = "<PROJECT-ID>"
  auth_database_name = "$external"
  aws_iam_type       = "ROLE"

  roles {
    role_name     = "readAnyDatabase"
    database_name = "admin"
  }

  labels {
    key   = "%s"
    value = "%s"
  }

  scopes {
    name   = "My cluster name"
    type = "CLUSTER"
  }
}
```


## Example of how to create a OIDC federated authentication user
```terraform
resource "mongodbatlas_database_user" "test" {
  username           = "64d613677e1ad50839cce4db/testUserOr"
  project_id         = "6414908c207f4d22f4d8f232"
  auth_database_name = "admin"
  oidc_auth_type     = "IDP_GROUP"

  roles {
    role_name     = "readWriteAnyDatabase"
    database_name = "admin"
  }
}
```
`username` format: Atlas OIDC IdP ID (found in federation settings), followed by a '/', followed by the IdP group name

Note: OIDC support is only avalible starting in [MongoDB 7.0](https://www.mongodb.com/evolved#mdbsevenzero) or later. To learn more, see the [MongoDB Atlas documentation](https://www.mongodb.com/docs/atlas/security-oidc/).


### Further Examples
- [Database User](https://github.com/mongodb/terraform-provider-mongodbatlas/tree/v2.7.0/examples/mongodbatlas_database_user)

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `auth_database_name` (String)
- `project_id` (String)
- `username` (String)

### Optional

- `aws_iam_type` (String)
- `description` (String)
- `labels` (Block Set) (see [below for nested schema](#nestedblock--labels))
- `ldap_auth_type` (String)
- `oidc_auth_type` (String)
- `password` (String, Sensitive)
- `roles` (Block Set) (see [below for nested schema](#nestedblock--roles))
- `scopes` (Block Set) (see [below for nested schema](#nestedblock--scopes))
- `x509_type` (String)

### Read-Only

- `id` (String) The ID of this resource.

<a id="nestedblock--labels"></a>
### Nested Schema for `labels`

Optional:

- `key` (String)
- `value` (String)


<a id="nestedblock--roles"></a>
### Nested Schema for `roles`

Required:

- `database_name` (String)
- `role_name` (String)

Optional:

- `collection_name` (String)


<a id="nestedblock--scopes"></a>
### Nested Schema for `scopes`

Optional:

- `name` (String)
- `type` (String)

## Import

Database users can be imported using project ID, username, and auth database name in the format:

1. `project_id`-`username`-`auth_database_name` Doesn't  work if `-` is used in both the `username` and the `auth_database_name`. For example `my-username` and `my-db` should use (2).
2. `project_id`/`username`/`auth_database_name` Works when neither `username` nor `auth_database_name` use `/`.

```
terraform import mongodbatlas_database_user.my_user 1112222b3bf99403840e8934-my_user-admin # (1)
terraform import mongodbatlas_database_user.my_user 1112222b3bf99403840e8934/my-username-dash/my-db-name # (2)
```

~> **NOTE:** Terraform will want to change the password after importing the user if a `password` argument is specified.
