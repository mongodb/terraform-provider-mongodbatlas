---
subcategory: "Service Accounts"
---

# Resource: mongodbatlas_service_account

`mongodbatlas_service_account` provides a Service Account resource. The resource lets you create, update, delete, and import a Service Account for the specified Organization.

~> **IMPORTANT WARNING:** Managing Service Accounts with Terraform will expose sensitive organizational secrets in Terraform's state. We suggest following [Terraform's best practices](https://developer.hashicorp.com/terraform/language/state/sensitive-data).

## Example Usages

```terraform
resource "mongodbatlas_service_account" "this" {
  org_id                     = var.org_id
  name                       = "example-service-account"
  description                = "Example Service Account"
  roles                      = ["ORG_READ_ONLY"]
  secret_expires_after_hours = 2160 # 90 days
}

data "mongodbatlas_service_account" "this" {
  org_id    = var.org_id
  client_id = mongodbatlas_service_account.this.client_id
}

data "mongodbatlas_service_accounts" "this" {
  org_id = var.org_id
}

output "service_account_client_id" {
  value = mongodbatlas_service_account.this.client_id
}

output "service_account_name" {
  value = data.mongodbatlas_service_account.this.name
}

output "service_account_first_secret" {
  description = "The secret value of the first secret created with the service account. Only available after initial creation."
  value       = try(mongodbatlas_service_account.this.secrets[0].secret, null)
  sensitive   = true
}

output "service_accounts_results" {
  value = data.mongodbatlas_service_accounts.this.results
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `description` (String) Human readable description for the Service Account.
- `name` (String) Human-readable name for the Service Account. The name is modifiable and does not have to be unique.
- `org_id` (String) Unique 24-hexadecimal digit string that identifies the organization that contains your projects.
- `roles` (Set of String) A list of organization-level roles for the Service Account.

### Optional

- `secret_expires_after_hours` (Number) The expiration time of the new Service Account secret, provided in hours. The minimum and maximum allowed expiration times are subject to change and are controlled by the organization's settings. This attribute is required when creating the Service Account and you cannot update it later.

### Read-Only

- `client_id` (String) The Client ID of the Service Account.
- `created_at` (String) The date that the Service Account was created on. This parameter expresses its value in the ISO 8601 timestamp format in UTC.
- `secrets` (Attributes List) A list of secrets associated with the specified Service Account. (see [below for nested schema](#nestedatt--secrets))

<a id="nestedatt--secrets"></a>
### Nested Schema for `secrets`

Read-Only:

- `created_at` (String) The date that the secret was created on. This parameter expresses its value in the ISO 8601 timestamp format in UTC.
- `expires_at` (String) The date for the expiration of the secret. This parameter expresses its value in the ISO 8601 timestamp format in UTC.
- `last_used_at` (String) The last time the secret was used. This parameter expresses its value in the ISO 8601 timestamp format in UTC.
- `masked_secret_value` (String) The masked Service Account secret.
- `secret` (String, Sensitive) The secret for the Service Account. It will be returned only the first time after creation.
- `secret_id` (String) Unique 24-hexadecimal digit string that identifies the secret.

## Import 
Import the Service Account resource by using the Organization ID and Client ID in the format `ORG_ID/CLIENT_ID`, e.g.
```
$ terraform import mongodbatlas_service_account.test 6117ac2fe2a3d04ed27a987v/mdb_sa_id_1234567890abcdef12345678
```

For more information, see [Create One Organization Service Account](https://www.mongodb.com/docs/api/doc/atlas-admin-api-v2/operation/operation-createorgserviceaccount) in the MongoDB Atlas API documentation.
