---
subcategory: "Service Accounts"
---

# Resource: mongodbatlas_project_service_account

`mongodbatlas_project_service_account` provides a Project Service Account resource. The resource lets you create, update, delete, and import a Service Account for the specified Project.

~> **IMPORTANT WARNING:** Managing Service Accounts with Terraform **exposes sensitive organizational secrets** in Terraform's state. We suggest following [Terraform's best practices](https://developer.hashicorp.com/terraform/language/state/sensitive-data).

~> **IMPORTANT:** The use of `mongodbatlas_project_service_account` resource is not recommended for users with Organization Owner permissions. Instead, use the [`mongodbatlas_service_account`](service_account.md) and [`mongodbatlas_service_account_project_assignment`](service_account_project_assignment.md) resources to manage Service Account project assignments.

~> **IMPORTANT:** Deleting a `mongodbatlas_project_service_account` resource unassigns the associated Service Account from the project, but doesn't delete it from the organization.

## Example Usages

```terraform
resource "mongodbatlas_project_service_account" "this" {
  project_id                 = var.project_id
  name                       = "example-project-service-account"
  description                = "Example Project Service Account"
  roles                      = ["GROUP_READ_ONLY"]
  secret_expires_after_hours = 2160 # 90 days
}

data "mongodbatlas_project_service_account" "this" {
  project_id = var.project_id
  client_id  = mongodbatlas_project_service_account.this.client_id
}

data "mongodbatlas_project_service_accounts" "this" {
  project_id = var.project_id
}

output "service_account_client_id" {
  value = mongodbatlas_project_service_account.this.client_id
}

output "service_account_name" {
  value = data.mongodbatlas_project_service_account.this.name
}

output "service_account_first_secret" {
  description = "The secret value of the first secret created with the service account. Available only immediately after initial creation."
  value       = try(mongodbatlas_project_service_account.this.secrets[0].secret, null)
  sensitive   = true
}

output "service_accounts_results" {
  value = data.mongodbatlas_project_service_accounts.this.results
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `description` (String) Human readable description for the Service Account.
- `name` (String) Human-readable name for the Service Account. The name is modifiable and does not have to be unique.
- `project_id` (String) Unique 24-hexadecimal digit string that identifies your project.
- `roles` (Set of String) A list of project-level roles for the Service Account.

### Optional

- `secret_expires_after_hours` (Number) The expiration time of the new Service Account secret, provided in hours. The minimum and maximum allowed expiration times are subject to change and are controlled by the organization's settings. This attribute is required when creating the Service Account and you cannot update it later.

### Read-Only

- `client_id` (String) The Client ID of the Service Account.
- `created_at` (String) The date that the Service Account was created on. This parameter expresses its value in the ISO 8601 timestamp format in UTC.
- `secrets` (Attributes List) A list of secrets associated with the specified Service Account. (see [below for nested schema](#nestedatt--secrets))

<a id="nestedatt--secrets"></a>
### Nested Schema for `secrets`

Read-Only:

- `created_at` (String) The date that the secret was created on. This parameter expresses its value in the ISO 8601 timestamp format in UTC.
- `expires_at` (String) The date for the expiration of the secret. This parameter expresses its value in the ISO 8601 timestamp format in UTC.
- `last_used_at` (String) The last time the secret was used. This parameter expresses its value in the ISO 8601 timestamp format in UTC.
- `masked_secret_value` (String) The masked Service Account secret.
- `secret` (String, Sensitive) The secret for the Service Account. It will be returned only the first time after creation.
- `secret_id` (String) Unique 24-hexadecimal digit string that identifies the secret.

## Import 
Import the Project Service Account resource by using the Project ID and Client ID in the format `PROJECT_ID/CLIENT_ID`, e.g.
```
$ terraform import mongodbatlas_project_service_account.test 6117ac2fe2a3d04ed27a987v/mdb_sa_id_1234567890abcdef12345678
```

For more information, see [Create One Project Service Account](https://www.mongodb.com/docs/api/doc/atlas-admin-api-v2/operation/operation-creategroupserviceaccount) in the MongoDB Atlas API documentation.
