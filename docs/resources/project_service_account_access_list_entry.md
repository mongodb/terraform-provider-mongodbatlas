---
subcategory: "Service Accounts"
---

# Resource: mongodbatlas_project_service_account_access_list_entry

`mongodbatlas_project_service_account_access_list_entry` provides a Project Service Account Access List entry resource. The resource lets you create, delete, and import a Project Service Account Access List entry for the specified Project.

~> **IMPORTANT:** When you remove an entry from the access list, existing connections from the removed address(es) may remain open for a variable amount of time. How much time passes before Atlas closes the connection depends on several factors, including how the connection was established, the particular behavior of the application or driver using the address, and the connection protocol (e.g., TCP or UDP). This is particularly important to consider when changing an existing IP address or CIDR block as they cannot be updated via the Provider, hence a change will force the destruction and recreation of entries.

~> **IMPORTANT WARNING:** Managing Service Accounts with Terraform **exposes sensitive organizational secrets** in Terraform's state. We suggest following [Terraform's best practices](https://developer.hashicorp.com/terraform/language/state/sensitive-data).

## Example Usages

```terraform
resource "mongodbatlas_project_service_account" "this" {
  project_id                 = var.project_id
  name                       = "example-project-service-account"
  description                = "Example Project Service Account"
  roles                      = ["GROUP_READ_ONLY"]
  secret_expires_after_hours = 2160 # 90 days
}

# Add IP Access List Entry to Project Service Account using CIDR Block
resource "mongodbatlas_project_service_account_access_list_entry" "cidr" {
  project_id = var.project_id
  client_id  = mongodbatlas_project_service_account.this.client_id
  cidr_block = "1.2.3.4/32"
}

# Add IP Access List Entry to Project Service Account using IP Address
resource "mongodbatlas_project_service_account_access_list_entry" "ip" {
  project_id = var.project_id
  client_id  = mongodbatlas_project_service_account.this.client_id
  ip_address = "2.3.4.5"
}

# Data source to read a single Project Service Account Access List entry
data "mongodbatlas_project_service_account_access_list_entry" "this" {
  project_id = mongodbatlas_project_service_account_access_list_entry.cidr.project_id
  client_id  = mongodbatlas_project_service_account_access_list_entry.cidr.client_id
  cidr_block = mongodbatlas_project_service_account_access_list_entry.cidr.cidr_block
}

output "access_list_entry_cidr_block" {
  value = data.mongodbatlas_project_service_account_access_list_entry.this.cidr_block
}

# Data source to read all Project Service Account Access List entries
data "mongodbatlas_project_service_account_access_list_entries" "this" {
  project_id = mongodbatlas_project_service_account.this.project_id
  client_id  = mongodbatlas_project_service_account.this.client_id

  depends_on = [
    mongodbatlas_project_service_account_access_list_entry.cidr,
    mongodbatlas_project_service_account_access_list_entry.ip
  ]
}

output "all_access_list_entries" {
  value = data.mongodbatlas_project_service_account_access_list_entries.this.results
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `client_id` (String) The Client ID of the Service Account.
- `project_id` (String) Unique 24-hexadecimal digit string that identifies the project.

### Optional

- `cidr_block` (String) Range of IP addresses in CIDR notation to be added to the access list. You can set a value for this parameter or **ip_address**, but not for both.
- `ip_address` (String) IP address to be added to the access list. You can set a value for this parameter or **cidr_block**, but not for both.

### Read-Only

- `created_at` (String) Date the entry was added to the access list. This attribute expresses its value in the ISO 8601 timestamp format in UTC.
- `last_used_address` (String) Network address that issued the most recent request to the API.
- `last_used_at` (String) Date when the API received the most recent request that originated from this network address.
- `request_count` (Number) The number of requests that has originated from this network address.

## Import

Project Service Account Access List entries can be imported using the `project_id`, `client_id` and `cidr_block` or `ip_address`, e.g.

```
$ terraform import mongodbatlas_project_service_account_access_list_entry.test 5d0f1f74cf09a29120e123cd-mdb_sa_id_1234567890abcdef12345678-10.242.88.0/21
```

For more information, see [Add Access List Entries for One Project Service Account](https://www.mongodb.com/docs/api/doc/atlas-admin-api-v2/operation/operation-creategroupserviceaccountaccesslist) in the MongoDB Atlas API documentation.
