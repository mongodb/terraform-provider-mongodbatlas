# Data Source: mongodbatlas_resource_policy

`mongodbatlas_resource_policy` describes a resource policy in an organization.

-> **NOTE**: Resource Policies are currently in Private Preview. To use this feature, you must take the following actions:
1. Enable the `Atlas Resource Policies` Preview Feature in your organization (contact [MongoDB Support](https://www.mongodb.com/services/support)).
2. Enable the [Preview Features](https://github.com/mongodb/terraform-provider-mongodbatlas?tab=readme-ov-file#preview-features) when running `terraform` commands.

## Example Usages
```terraform
resource "mongodbatlas_resource_policy" "project_ip_access_list" {
  org_id = var.org_id
  name   = "forbid-access-from-anywhere"

  policies = [
    {
      body = <<EOF
        forbid (
                principal,
                action == cloud::Action::"project.edit",
                resource
        )
                when {
                context.project.ipAccessList.contains(ip("0.0.0.0/0"))
        };
EOF
    },
  ]
}

resource "mongodbatlas_resource_policy" "cloud_provider" {
  org_id = var.org_id
  name   = "forbid-cloud-provider"
  policies = [
    {
      body = templatefile("${path.module}/cloud-provider.cedar", {
        CLOUD_PROVIDER = "azure"
      })
    },
    {
      body = templatefile("${path.module}/cloud-provider.cedar", {
        CLOUD_PROVIDER = "aws"
      })
    },
  ]
}

data "cedar_policyset" "cloud_region" {
  policy {
    any_principal = true
    effect        = "forbid"
    action = {
      type = " cloud::Action"
      id   = "cluster.createEdit"
    }
    any_resource = true
    when {
      text = "context.cluster.regions.contains(cloud::region::\"gcp:us-east1\")"
    }
  }
}

resource "mongodbatlas_resource_policy" "cloud_region" {
  org_id = var.org_id
  name   = "forbid-cloud-region"
  policies = [
    {
      body = data.cedar_policyset.cloud_region.text
    },
  ]
}


data "mongodbatlas_resource_policy" "project_ip_access_list" {
  org_id = mongodbatlas_resource_policy.project_ip_access_list.org_id
  id     = mongodbatlas_resource_policy.project_ip_access_list.id
}

data "mongodbatlas_resource_policies" "this" {
  org_id = data.mongodbatlas_resource_policy.project_ip_access_list.org_id

  depends_on = [mongodbatlas_resource_policy.project_ip_access_list, mongodbatlas_resource_policy.cloud_provider, mongodbatlas_resource_policy.cloud_region]
}


output "policy_ids" {
  value = { for policy in data.mongodbatlas_resource_policies.this.resource_policies : policy.name => policy.id }
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `id` (String) Unique 24-hexadecimal digit string that identifies an Atlas resource policy.
- `org_id` (String) Unique 24-hexadecimal digit string that identifies the organization that contains your projects. Use the [/orgs](#tag/Organizations/operation/listOrganizations) endpoint to retrieve all organizations to which the authenticated user has access.

### Read-Only

- `created_by_user` (Attributes) The user that last updated the Atlas resource policy. (see [below for nested schema](#nestedatt--created_by_user))
- `created_date` (String) Date and time in UTC when the Atlas resource policy was created.
- `last_updated_by_user` (Attributes) The user that last updated the Atlas resource policy. (see [below for nested schema](#nestedatt--last_updated_by_user))
- `last_updated_date` (String) Date and time in UTC when the Atlas resource policy was last updated.
- `name` (String) Human-readable label that describes the Atlas resource policy.
- `policies` (Attributes List) List of policies that make up the Atlas resource policy. (see [below for nested schema](#nestedatt--policies))
- `version` (String) A string that identifies the version of the Atlas resource policy.

<a id="nestedatt--created_by_user"></a>
### Nested Schema for `created_by_user`

Read-Only:

- `id` (String) Unique 24-hexadecimal character string that identifies a user.
- `name` (String) Human-readable label that describes a user.


<a id="nestedatt--last_updated_by_user"></a>
### Nested Schema for `last_updated_by_user`

Read-Only:

- `id` (String) Unique 24-hexadecimal character string that identifies a user.
- `name` (String) Human-readable label that describes a user.


<a id="nestedatt--policies"></a>
### Nested Schema for `policies`

Read-Only:

- `body` (String) A string that defines the permissions for the policy. The syntax used is the Cedar Policy language.
- `id` (String) Unique 24-hexadecimal character string that identifies the policy.

