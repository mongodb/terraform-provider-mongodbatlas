---
subcategory: "Projects"
---

# Data Source: mongodbatlas_project_ip_access_list

`mongodbatlas_project_ip_access_list` describes an IP Access List entry resource. The access list grants access from IPs, CIDRs or AWS Security Groups (if VPC Peering is enabled) to clusters within the Project.

-> **NOTE:** Groups and projects are synonymous terms. You may find `groupId` in the official documentation.

## Example Usage

### Using CIDR Block
```terraform
resource "mongodbatlas_project_ip_access_list" "this" {
  project_id = var.project_id
  cidr_block = "1.2.3.4/32"
  comment    = "cidr block test"
}

data "mongodbatlas_project_ip_access_list" "this" {
	project_id = mongodbatlas_project_ip_access_list.this.project_id
	cidr_block = mongodbatlas_project_ip_access_list.this.cidr_block
}
```

### Using IP Address
```terraform
resource "mongodbatlas_project_ip_access_list" "this" {
  project_id = var.project_id
  ip_address = "2.3.4.5"
  comment    = "ip address test"
}

data "mongodbatlas_project_ip_access_list" "this" {
	project_id = mongodbatlas_project_ip_access_list.this.project_id
	ip_address = mongodbatlas_project_ip_access_list.this.ip_address
}
```

### Using an AWS Security Group
```terraform
resource "mongodbatlas_network_container" "this" {
  project_id       = var.project_id
  atlas_cidr_block = "192.168.208.0/21"
  provider_name    = "AWS"
  region_name      = "US_EAST_1"
}

resource "mongodbatlas_network_peering" "this" {
  project_id             = var.project_id
  container_id           = mongodbatlas_network_container.this.container_id
  accepter_region_name   = "us-east-1"
  provider_name          = "AWS"
  route_table_cidr_block = "172.31.0.0/16"
  vpc_id                 = "vpc-0d93d6f69f1578bd8"
  aws_account_id         = "232589400519"
}

resource "mongodbatlas_project_ip_access_list" "this" {
  project_id         = var.project_id
  aws_security_group = "sg-0026348ec11780bd1"
  comment            = "AWS Security Group test"

  depends_on = [mongodbatlas_network_peering.this]
}

data "mongodbatlas_project_ip_access_list" "this" {
	project_id = mongodbatlas_project_ip_access_list.this.project_id
	aws_security_group = mongodbatlas_project_ip_access_list.this.aws_security_group
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `project_id` (String) Unique 24-hexadecimal digit string that identifies your project.

### Optional

- `aws_security_group` (String) Unique identifier of the AWS security group to add to the access list. Mutually exclusive with `cidr_block` and `ip_address`.
- `cidr_block` (String) Range of IP addresses in CIDR notation to be added to the access list. Mutually exclusive with `ip_address` and `aws_security_group`.
- `ip_address` (String) Single IP address to be added to the access list. Mutually exclusive with `cidr_block` and `aws_security_group`.

### Read-Only

- `comment` (String) Remark that explains the purpose or scope of this IP access list entry.
- `id` (String) Unique identifier used for terraform for internal management.

For more information, see [MongoDB Atlas API Reference](https://docs.atlas.mongodb.com/reference/api/access-lists/).
